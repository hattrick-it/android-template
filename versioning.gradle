/*
Used to update Build Number and Commit Changes to Repository
* */

def getVersionProps() {
    def versionPropsFile = file('gradle.properties')
    if (!versionPropsFile.exists()) {
        versionPropsFile.createNewFile()
    }
    def versionProps = new Properties()
    versionPropsFile.withInputStream {versionProps.load(it) }
    return versionProps
}

private void commitSemanticRelease(versionName) {
    Process addChanges = ['git', 'add', 'gradle.properties'].execute(null, project.rootDir)
    addChanges.waitForProcessOutput(System.out, System.err)

    Process createCommit = ['git', 'commit', "-m Release ${versionName}"].execute(null,  project.rootDir)
    createCommit.waitForProcessOutput(System.out, System.err)
}

private def getAppVersionCode() { getVersionProps()['appVersionCode'].toInteger() }

private void saveSemanticRelease(versionName) {
    def versionProps = getVersionProps()
    versionProps['appVersionName'] = versionName
    versionProps['appVersionCode'] = (getAppVersionCode() + 1).toString()
    versionProps.store(file('gradle.properties').newWriter(), null)
    commitSemanticRelease(versionName)
}

task bumperSemanticRelease() {
    group = 'bumper'
    doLast {
        def versionName = project.hasProperty('bumperVersionName') ? bumperVersionName : '1.0.0'
        saveSemanticRelease(versionName)
    }
}